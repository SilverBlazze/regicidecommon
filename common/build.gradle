apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.squareup.sqldelight'

group = 'com.oracle.regicidecommon'
version = 1.0

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


kotlin {
    targets {
        fromPreset(presets.android, 'android')

        def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
        def iosPreset = (buildForDevice) ? presets.iosArm64 : presets.iosX64
        fromPreset(iosPreset, 'ios') {
            binaries {
                framework {
                    // Disable bitcode embedding for the simulator build.
                    if (!buildForDevice) {
                        embedBitcode("disable")
                    }
                }
            }
        }
    }

    sourceSets {
        androidMain {
            dependencies {
                // Kotlin
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

                // Coroutines
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

                // Ktor
                implementation "io.ktor:ktor-client-android:$ktor_version"
                implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-serialization-jvm:$ktor_version"
                implementation "org.slf4j:slf4j-android:$slf4j_version"

                // Serialize
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

                // SQLDelight
                implementation "com.squareup.sqldelight:android-driver:$sqldelight_version"
            }
        }
        iosMain {
            dependencies {
                // Coroutines
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"

                // Ktor
                implementation "io.ktor:ktor-client-ios:$ktor_version"
                implementation "io.ktor:ktor-client-json-native:$ktor_version"
                implementation "io.ktor:ktor-client-serialization-native:$ktor_version"
                implementation "io.ktor:ktor-client-logging-native:$ktor_version"

                // Serialize
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"

                // SQLDelight
                implementation "com.squareup.sqldelight:ios-driver:$sqldelight_version"
            }
        }
        commonMain {
            dependencies {
                // Kotlin
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

                // Coroutines
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"

                // Kodein
                implementation "org.kodein.di:kodein-di-core:$kodein_version"
                implementation "org.kodein.di:kodein-di-erased:$kodein_version"

                // Ktor
                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "io.ktor:ktor-client-json:$ktor_version"
                implementation "io.ktor:ktor-client-serialization:$ktor_version"
                implementation "io.ktor:ktor-client-logging:$ktor_version"

                // Serialize
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"

                // SQLDelight
                implementation "com.squareup.sqldelight:runtime:$sqldelight_version"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
            }
        }
    }
}

sqldelight {
    PreferencesDatabase {
        packageName = "com.oracle.dv.preferences"
        sourceFolders = ["prefs"]
        schemaOutputDirectory = file("src/main/database/migrations")
    }
    OacDatabase {
        packageName = "com.oracle.dv.datasets"
        sourceFolders = ["oac"]
        schemaOutputDirectory = file("src/main/database/migrations")
    }
}

task copyFramework {
    def buildType = project.findProperty("kotlin.build.type") ?: "DEBUG"
    dependsOn "link${buildType.toLowerCase().capitalize()}FrameworkIos"

    doLast {
        def srcFile = kotlin.targets.ios.binaries.getFramework(buildType).outputFile
        def targetDir = "/Users/alexacosta/Documents/Dev/iosdv/Oracle DV"
        copy {
            from srcFile.parent
            into targetDir
            include 'common.framework/**'
            include 'common.framework.dSYM'
        }
    }
}

task iosTest {
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn 'linkTestDebugExecutableIos'
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.binaries.getExecutable('test', 'DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}
